/**
    "name": "Asynchronous FIFO"
    "description": "A FIFO (First In First Out) with independent clocks for writing and reading. These are useful for crossing clock domains."
**/
/******************************************************************************

   The MIT License (MIT)

   Copyright (c) 2026 Alchitry

   Permission is hereby granted, free of charge, to any person obtaining a copy
   of this software and associated documentation files (the "Software"), to deal
   in the Software without restriction, including without limitation the rights
   to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
   copies of the Software, and to permit persons to whom the Software is
   furnished to do so, subject to the following conditions:

   The above copyright notice and this permission notice shall be included in
   all copies or substantial portions of the Software.

   THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
   IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
   FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
   AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
   LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
   OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
   THE SOFTWARE.

   *****************************************************************************

   This is an asynchronous fifo. That means it has two independently clocked
   interfaces that allow you to write data from one clock domain and read
   it from another.

   This is a first-word-fall-through fifo meaning that dout is valid whenever
   empty is 0. If you want to perform a read, simply check if empty is 0 and
   if it is read the value of dout and set rget high to advance to the next
   value.

   SYNC_STAGES is used to set the number of chained dffs used when crossing
   clock domains. 3 is usually a good default, but fewer can be used when latency
   is a priority over reliability.
*/

module async_fifo #(
    WIDTH ~ 4 : WIDTH > 0, // Size of the data
    ENTRIES ~ 8 : ENTRIES == $pow(2,$clog2(ENTRIES)), // ENTRIES must be a power of 2
    SYNC_STAGES = 3 : SYNC_STAGES >= 2 // Number of synchronizing stages
)(
    input wclk, // write clock
    input wrst, // write reset
    input din [WIDTH], // write data
    input wput, // write flag (1 = write)
    output full, // full flag (1 = full)

    input rclk, // read clock
    input rrst, // read reset
    output dout [WIDTH], // read data
    input rget, // data read flag (1 = get next entry)
    output empty // empty flag (1 = empty)
) {

    const ADDR_SIZE = $clog2(ENTRIES) // size of RAM address

    // write clock domain
    .clk(wclk), .rst(wrst) {
        dff waddr[ADDR_SIZE] // write address
        dff wsync[SYNC_STAGES][ADDR_SIZE] // write sync dffs
        dff gwsync[ADDR_SIZE]
    }

    // read clock domain
    .clk(rclk), .rst(rrst) {
        dff raddr[ADDR_SIZE] // read address
        dff rsync[SYNC_STAGES][ADDR_SIZE] // read sync dffs
        dff grsync[ADDR_SIZE]
    }

    // dual port RAM
    simple_dual_port_ram ram (#WIDTH(WIDTH), #ENTRIES(ENTRIES))

    sig waddr_gray[ADDR_SIZE] // gray-encoded version of waddr
    sig wnext_gray[ADDR_SIZE] // gray-encoded version of waddr + 1
    sig raddr_gray[ADDR_SIZE] // gray-encoded version of raddr
    sig wrdy // write ready flag
    sig rrdy // read ready flag

    sig wnext[ADDR_SIZE]

    always {
        // connect the clocks
        ram.wclk = wclk
        ram.rclk = rclk

        ram.write_enable = 0 // default to not writing

        // convert the various values to their gray-encoded versions
        wnext = waddr.q + 1
        waddr_gray = waddr.q[-1:1] ^ waddr.q
        wnext_gray = wnext[-1:1] ^ wnext
        raddr_gray = raddr.q[-1:1] ^ raddr.q

        gwsync.d = waddr_gray
        grsync.d = raddr_gray

        // cross clock domains with synchronizers
        rsync.d = c{rsync.q[-2:0], {gwsync.q}}
        wsync.d = c{wsync.q[-2:0], {grsync.q}}

        // if next write address isn't the read address we can write
        wrdy = wnext_gray != wsync.q[-1]

        // if the current read address isn't the write address we can read
        rrdy = raddr_gray != rsync.q[-1]

        // invert ready signals for full and empty
        full = !wrdy
        empty = !rrdy

        // connect RAM ports
        ram.waddr = waddr.q
        ram.raddr = raddr.q
        ram.write_data = din

        // if we have space and should write
        if (wput && wrdy) {
            waddr.d = waddr.q + 1 // increment write address
            ram.write_enable = 1 // set write flag
        }

        // if we have data and a read request
        if (rget && rrdy) {
            raddr.d = raddr.q + 1 // increment read address
            ram.raddr = raddr.q + 1 // set to next address to keep up with reads
        }

        dout = ram.read_data // output is directly from RAM
    }
}