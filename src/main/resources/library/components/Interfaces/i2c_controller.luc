/**
    "name": "I2C Controller"
    "description": "I2C controller (master) that can be used to talk to I2C devices. It has a configurable clock divider."
**/
/******************************************************************************

   The MIT License (MIT)

   Copyright (c) 2026 Alchitry

   Permission is hereby granted, free of charge, to any person obtaining a copy
   of this software and associated documentation files (the "Software"), to deal
   in the Software without restriction, including without limitation the rights
   to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
   copies of the Software, and to permit persons to whom the Software is
   furnished to do so, subject to the following conditions:

   The above copyright notice and this permission notice shall be included in
   all copies or substantial portions of the Software.

   THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
   IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
   FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
   AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
   LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
   OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
   THE SOFTWARE.

   *****************************************************************************

   I2C Controller: This is an I2C controller interface. With this
   you can communicate to multiple I2C peripheral devices.

   Each I2C transaction begins with the start event. This
   is triggered by setting the start input high for one
   clock cycle. You can then send as many read and write
   commands as you want. To end a transaction, you need
   to set the stop input high for one clock cycle.

   Each time a command is sent (start, stop,
   read, or write) you must make sure the busy flag is
   not set before issuing the command. Otherwise, your
   command will be ignored.

   Typically, a transaction will start by writing a byte
   that contains the ID of the device to talk to and
   if the operation is a read or write to it. See your
   device's datasheet for what to send.

   After that, multiple reads or writes can take place
   depending on the operation.

   When performing a read, you must specify if the read
   should be acknowledged by setting ack_read to 1 to
   acknowledge or 0 to not.

   When performing a write, ack_write will tell if your
   write was acknowledged by the peripheral device.
*/

module i2c_controller #(
    CLK_DIV = 8 : CLK_DIV >= 4 // divide clock by 2^CLK_DIV, must be 4 or more
)(
    input clk,          // clock
    input rst,          // reset

    // Device interface
    inout sda,          // bi-directional
    inout scl,          // data clock

    // Control signals
    input start,        // send start signal
    input stop,         // send stop signal

    // Write interface
    input data_in[8],   // data to write
    input write,        // start write signal (1 = start write)
    output ack_write,   // was the last write acknowledged (1 = ack, 0 = no ack)

    // Read interface
    output data_out[8], // data read
    output out_valid,   // output valid (1 = valid)
    input read,         // start read signal (1 = start read)
    input ack_read,     // should read be acknowledged (1 = ack, 0 = no ack)

    // Status
    output busy         // I2C is busy (1 = busy, 0 = idle)
) {
    enum States {WAIT_CMD, START, READ, WRITE, STOP, WAIT}

    .clk(clk){
        .rst(rst) {
            dff state[$width(States)](#INIT(States.WAIT_CMD))
            dff bus_active
        }
        dff data[8]           // data to send or data received
        dff out_valid_reg     // out_valid buffer
        dff scl_reg[CLK_DIV]  // clock divider
        dff bit_ctr[4]        // bit counter
        dff ack_write_reg     // ack_write buffer
        dff ack_read_reg      // ack_read buffer
        dff scl_sync[2]       // synchronizer for scl
        dff sda_sync[2]       // synchronizer for sda
    }

    const RISING_EDGE = $width(scl_reg.q)x{1b0}
    const TOP_MIDDLE = c{2b00, ($width(scl_reg.q)-2)x{1b1}}
    const FALLING_EDGE = c{1b1, ($width(scl_reg.q)-1)x{1b0}}
    const BOTTOM_MIDDLE = c{2b11, ($width(scl_reg.q)-2)x{1b0}}

    always {
        // the clock is the MSB of scl_reg inverted
        scl = scl_reg.q[-1] ? b0 : bz

        // sync the inputs to the clock for safety
        scl_sync.d = c{scl_sync.q[-2:0], scl}
        sda_sync.d = c{sda_sync.q[-2:0], sda}

        // stretching the clock if it should be high but is 0
        sig clock_stretching = scl_reg.q[-1] == 0 && scl_sync.q[-1] == 0

        // we are busy if we aren't waiting for a command
        busy = state.q != States.WAIT_CMD

        // write the outputs
        data_out = data.q
        out_valid = out_valid_reg.q
        ack_write = ack_write_reg.q

        // defaults
        out_valid_reg.d = 0 // not valid
        sig sda_value = 1 // default to floating pin

        case (state.q) {
            States.WAIT_CMD: // Wait for command
                bit_ctr.d = 0 // reset bit counter

                // idle clock low when active and high when inactive
                scl_reg.d = bus_active.q ? FALLING_EDGE : RISING_EDGE

                if (write) {                      // new write command
                    scl_reg.d = BOTTOM_MIDDLE + 1 // set clock counter to 3/4 full
                    data.d = data_in              // save the data to send
                    state.d = States.WRITE        // enter write state
                } else if (read) {                // new read command
                    scl_reg.d = BOTTOM_MIDDLE     // set clock counter to 3/4 full
                    ack_read_reg.d = ack_read     // save if read should be acknowledged
                    state.d = States.READ         // enter read state
                } else if (stop) {                // stop command
                    bus_active.d = 0              // set bus inactive
                    scl_reg.d = BOTTOM_MIDDLE     // set clock counter to 3/4 full
                    state.d = States.STOP         // enter stop state
                } else if (start) {               // start command
                    bus_active.d = 1              // set bus active
                    scl_reg.d = TOP_MIDDLE        // set clock to 1/4 full
                    state.d = States.START        // enter start state
                }

            States.START:
                if (!clock_stretching)
                    scl_reg.d = scl_reg.q + 1 // increment the counter

                // if the clock counter is less than 1/4 full or more than 3/4 full
                sda_value = scl_reg.q <= TOP_MIDDLE || scl_reg.q >= BOTTOM_MIDDLE

                // if the clock counter is 3/4 full
                if (scl_reg.q == BOTTOM_MIDDLE)
                    state.d = States.WAIT_CMD // enter wait command state

            States.READ: // Read in a byte
                // if we at the end of the 8th bit or later
                if (bit_ctr.q == 9 || (bit_ctr.q == 8 && (scl_reg.q >= BOTTOM_MIDDLE || scl_reg.q <= TOP_MIDDLE))) {
                    sda_value = ~ack_read_reg.q // write the acknowledge bit
                }

                // increment the clock counter
                if (!clock_stretching)
                    scl_reg.d = scl_reg.q + 1

                if (scl_reg.q == TOP_MIDDLE) {
                    // make sure we don't get stuck here and increment bit_ctr multiple times
                    scl_reg.d = scl_reg.q + 1

                    // if it's a data bit
                    if (bit_ctr.q < 8)
                        data.d = c{data.q[6:0], sda_sync.q[-1]} // shift it in

                    // increment the bit counter
                    bit_ctr.d = bit_ctr.q + 1

                } else if (scl_reg.q == BOTTOM_MIDDLE && bit_ctr.q == 9) {
                    out_valid_reg.d = 1       // output data is valid
                    state.d = States.WAIT_CMD // wait for next command
                }

            States.WRITE: // Write a byte
                // if we aren't on the acknowledge bit
                if (bit_ctr.q != 8)
                    sda_value = data.q[7]     // write the MSB of data

                // increment the clock counter
                if (!clock_stretching)
                    scl_reg.d = scl_reg.q + 1

                // if the clock counter is 1/2 full
                if (scl_reg.q == BOTTOM_MIDDLE) {
                    // shift data
                    data.d = c{data.q[6:0], 1bx}

                    // increment the bit counter
                    bit_ctr.d = bit_ctr.q + 1

                    // if we're on the last bit
                    if (bit_ctr.q == 8) {
                        state.d = States.WAIT_CMD // wait for the next command
                    }
                }

                // if we're on the last bit
                if (scl_reg.q == TOP_MIDDLE && bit_ctr.q == 8) {
                    ack_write_reg.d = ~sda_sync.q // read the acknowledge bit
                }

            States.STOP: // Send stop command
                sda_value = 0
                if (!clock_stretching)
                    scl_reg.d = scl_reg.q + 1 // increment the clock counter

                if (scl_reg.q == TOP_MIDDLE) {
                    sda_value = 1           // set the output high
                    state.d = States.WAIT   // enter wait state
                    scl_reg.d = RISING_EDGE // reset clock counter
                }

            States.WAIT: // Wait 1/2 a clock period
                if (!clock_stretching)
                    scl_reg.d = scl_reg.q + 1 // increment the clock counter

                // if the clock counter is just under half full
                // We don't want it to reach 1/2 full because
                // the clock output would then go low
                if (scl_reg.q == FALLING_EDGE - 1){
                    state.d = States.WAIT_CMD // enter idle state
                    scl_reg.d = RISING_EDGE   // reset clock counter
                }

            default: // Should never enter here
                state.d = States.WAIT_CMD
        }

        sda = sda_value ? bz : b0 // only pull down
    }
}